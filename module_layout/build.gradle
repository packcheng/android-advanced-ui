if (rootProject.ext.isApp.isLayoutApp) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        if (rootProject.ext.isApp.isLayoutApp) {
            applicationId "${rootProject.ext.android.applicationId}layout"
        }

        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner rootProject.ext.android.androidJUnitRunner

        //ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // 使用dataBinding
    dataBinding {
        enabled = true
    }
    // 使用 JDK8
    compileOptions {
        sourceCompatibility rootProject.ext.compileOptions.sourceCompatibility
        targetCompatibility rootProject.ext.compileOptions.targetCompatibility
    }
    // 模块化资源文件
    sourceSets {
        main {
            if (rootProject.ext.isApp.isLayoutApp) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }
    // 模块化资源前缀
    resourcePrefix rootProject.ext.resourcePrefix["moduleLayout"]
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["androidx-junit"]
    androidTestImplementation rootProject.ext.dependencies["androidx-espresso-core"]

    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    implementation project(path: ':base')
}